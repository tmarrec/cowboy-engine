#version 460 core

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

struct PointLight
{
    vec3    color;
    float   radius;
    vec3    position;
};

struct Sphere
{
    vec3    c; // Center point
    float   r; // Radius 
};

struct Plane
{
    vec3    N;
    float   d;
};

struct Frustum
{
    vec3    N[4];
    float   d[4];
};

layout (std430, binding = 0) writeonly buffer FrustumBuffer
{
    Frustum gFrustumBuffer[];
};

uniform mat4 invProjection;
uniform mat4 view;

uniform int blockSize;
uniform int screenWidth;
uniform int screenHeight;

// Convert clip space coordinates to view space
vec4 clipToView(vec4 clip)
{
    vec4 view = clip * invProjection;
    view = view / view.w;
    return view;
}

// Convert screen space coordinates to view space
vec4 screenToView(vec4 screen)
{
    // Convert to normalized texture coordinates
    vec2 texCoord = screen.xy / vec2(screenWidth, screenHeight);
    // Convert to clip space
    vec4 clip = vec4(vec2(texCoord.x, 1.0f - texCoord.y) * 2.0f - 1.0f, screen.z, screen.w);

    return clipToView(clip);
}

Plane computePlane(vec3 p0, vec3 p1, vec3 p2)
{
    Plane plane;
    vec3 v0 = p1 - p0;
    vec3 v2 = p2 - p0;
    plane.N = normalize(cross(v0, v2));
    plane.d = dot(plane.N, p0);
    return plane;
}

void main()
{
    vec4 screenSpace[4];
    screenSpace[0] = vec4(gl_WorkGroupID.xy * blockSize,                                1.0f, 1.0f);
    screenSpace[1] = vec4(vec2(gl_WorkGroupID.x + 1, gl_WorkGroupID.y) * blockSize,     1.0f, 1.0f);
    screenSpace[2] = vec4(vec2(gl_WorkGroupID.x, gl_WorkGroupID.y + 1) * blockSize,     1.0f, 1.0f);
    screenSpace[3] = vec4(vec2(gl_WorkGroupID.x + 1, gl_WorkGroupID.y + 1) * blockSize, 1.0f, 1.0f);

    vec3 viewSpace[4];
    for (int i = 0; i < 4; ++i)
    {
        viewSpace[i] = screenToView(screenSpace[i]).xyz;
    }

    const vec3 eyePos = vec3(0,0,0); // Screenspace

    Plane p0 = computePlane(eyePos, viewSpace[2], viewSpace[0]);
    Plane p1 = computePlane(eyePos, viewSpace[1], viewSpace[3]);
    Plane p2 = computePlane(eyePos, viewSpace[0], viewSpace[1]);
    Plane p3 = computePlane(eyePos, viewSpace[3], viewSpace[2]);

    Frustum frustum;
    frustum.N[0] = p0.N;
    frustum.d[0] = p0.d;
    frustum.N[1] = p1.N;
    frustum.d[1] = p1.d;
    frustum.N[2] = p2.N;
    frustum.d[2] = p2.d;
    frustum.N[3] = p3.N;
    frustum.d[3] = p3.d;

    //if (gl_WorkGroupID.x < gl_GlobalInvocationID.x && gl_WorkGroupID.y < gl_GlobalInvocationID.y)
    {
        gFrustumBuffer[gl_GlobalInvocationID.x + (gl_GlobalInvocationID.y * (gl_NumWorkGroups.x * gl_WorkGroupSize.x))] = frustum;
    }
}
